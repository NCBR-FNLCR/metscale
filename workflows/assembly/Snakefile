'''
Author: Phillip Brooks, Charles Reid
Affiliation: UC Davis Lab for Data Intensive Biology
Objective: A Snakemake workflow to assemble quality trimmed reads using SPAdes and MEGAHIT  
Date: 2018-06-11
Documentation: docs/workflow_assembly.md
'''

from common.utils  import container_image_is_external, container_image_name
from os.path import join, isfile, dirname
import os, re
import pdb


############################################
# Assembly: default config

data_dir = config['data_dir']
sing_dir = config['sing_dir']
image_dir = 'images/read'
biocontainers = config['biocontainers']
taxclass = config['taxonomic_classification']
assembly = config['assembly']
readfilt = config['read_filtering']



###################################
# Assembly: build rules

# Skip to the very end of the file 
# to see the high-level build rules
# that trigger cascades of workflow
# tasks.


###################################
# Assembly: metaspades

# Post trimming pattern uses the {direction} wildcard.
# We have to explicitly expand {direction} to _1 and _2.
# (If we rely on wildards, _1 and _2 are two separate rules - bad.)

assembly_metaspades_input_pattern = join(data_dir, readfilt['read_patterns']['post_trimming_pattern'])
assembly_metaspades_input_pattern_sing = join(sing_dir, readfilt['read_patterns']['post_trimming_pattern'])

assembly_metaspades_input_fwd = assembly_metaspades_input_pattern.format(
            direction=readfilt['direction_labels']['forward'], 
            sample='{sample}', 
            qual='{qual}'
)
assembly_metaspades_input_rev = assembly_metaspades_input_pattern.format(
            direction=readfilt['direction_labels']['reverse'], 
            sample='{sample}', 
            qual='{qual}'
)

assembly_metaspades_input_fwd_sing = assembly_metaspades_input_pattern_sing.format(
            direction=readfilt['direction_labels']['forward'], 
            sample='{sample}', 
            qual='{qual}'
)
assembly_metaspades_input_rev_sing = assembly_metaspades_input_pattern_sing.format(
            direction=readfilt['direction_labels']['reverse'], 
            sample='{sample}', 
            qual='{qual}'
)



assembly_metaspades_outprefix = re.sub('.contigs.fa','',assembly['assembly_patterns']['metaspades_pattern'])
assembly_metaspades_output = join(data_dir, assembly['assembly_patterns']['metaspades_pattern'])
assembly_metaspades_output_sing = join(sing_dir, assembly['assembly_patterns']['metaspades_pattern'])

assembly_metaspades_logfile = re.sub('contigs.fa','log',assembly_metaspades_output)
assembly_metaspades_benchfile = re.sub('contigs.fa','benchmark.txt',assembly_metaspades_output)

spades_image = container_image_name(biocontainers, 'spades')


rule assembly_metaspades:
    """
    Perform read assembly of trimmed reads using metaspades.
    """
    input:
        fwd = assembly_metaspades_input_fwd,
        rev = assembly_metaspades_input_rev
    output:
        assembly_metaspades_output
    params:
        fwd_sing = assembly_metaspades_input_fwd_sing,
        rev_sing = assembly_metaspades_input_rev_sing,
        metaspades_output_expanded = assembly_metaspades_output_sing
    message: 
         '--- Assembling quality trimmed reads with SPAdes'
    singularity: 
        spades_image
    threads: 8    #TODO: hard coded values? 
    log: 
        assembly_metaspades_logfile
    benchmark:
        assembly_metaspades_benchfile
    shell:
        'metaspades.py -t {threads} '
        '-m 240 '
#        '-1 /{input.fwd} '
#        '-2 /{input.rev} '
         '-1 {params.fwd_sing} '
         '-2 {params.rev_sing} '
        '-o /{sing_dir} '
        '&& '
        'mv /{sing_dir}/contigs.fasta /{params.metaspades_output_expanded} '


###################################
# Assembly: megahit

assembly_megahit_input_pattern = join(data_dir, readfilt['read_patterns']['post_trimming_pattern'])
assembly_megahit_input_pattern_sing = join(sing_dir, readfilt['read_patterns']['post_trimming_pattern'])

assembly_megahit_input_fwd = assembly_megahit_input_pattern.format(
            direction=readfilt['direction_labels']['forward'], 
            sample='{sample}', 
            qual='{qual}'
)
assembly_megahit_input_rev = assembly_megahit_input_pattern.format(
            direction=readfilt['direction_labels']['reverse'], 
            sample='{sample}', 
            qual='{qual}'
)

assembly_megahit_input_fwd_sing = assembly_megahit_input_pattern_sing.format(
            direction=readfilt['direction_labels']['forward'], 
            sample='{sample}', 
            qual='{qual}'
)
assembly_megahit_input_rev_sing = assembly_megahit_input_pattern_sing.format(
            direction=readfilt['direction_labels']['reverse'], 
            sample='{sample}', 
            qual='{qual}'
)

assembly_megahit_outprefix = re.sub('.contigs.fa','',assembly['assembly_patterns']['megahit_pattern'])
assembly_megahit_output = join(data_dir, assembly['assembly_patterns']['megahit_pattern'])

assembly_megahit_logfile = re.sub('contigs.fa','log',assembly_megahit_output)
assembly_megahit_benchfile = re.sub('contigs.fa','benchmark.txt',assembly_megahit_output)

megahit_image = container_image_name(biocontainers, 'megahit')

def assembly_megahit_outprefix_sub(wildcards):
    return assembly_megahit_outprefix.format(**wildcards)

rule assembly_megahit:
    """
    Perform read assembly of trimmed reads using megahit.
    """
    input:
        fwd = assembly_megahit_input_fwd,
        rev = assembly_megahit_input_rev,
    output:
        assembly_megahit_output
    message: 
        '''--- Assembling quality trimmed reads with Megahit'''
    singularity:
        megahit_image
    threads: 8  #TODO: hard coded values
    log:
        assembly_megahit_logfile
    benchmark:
        assembly_megahit_benchfile
    params:
        assembly_megahit_outprefix_wc = assembly_megahit_outprefix_sub,
        fwd_sing = assembly_megahit_input_fwd_sing,
        rev_sing = assembly_megahit_input_rev_sing,
    shell:
        'rm -rf /{sing_dir}/{params.assembly_megahit_outprefix_wc} '
        '&& '
        'megahit -t 8 '  #TODO: {threads} only shows 1
     #   '--memory 0.20 '
     #   '-m 1654561219 '
        '-1 {params.fwd_sing} '
        '-2 {params.rev_sing} '
        '--out-prefix={params.assembly_megahit_outprefix_wc} '
        '-o /{sing_dir}/{params.assembly_megahit_outprefix_wc} '
        '&& '
        'mv '
        '/{sing_dir}/{params.assembly_megahit_outprefix_wc}/{params.assembly_megahit_outprefix_wc}.contigs.fa '
        '/{sing_dir}/{params.assembly_megahit_outprefix_wc}.contigs.fa '


###################################
# Assembly: quast statistics

assembly_quast_input = join(data_dir, assembly['assembly_patterns']['assembly_pattern'])
assembly_quast_input_sing = join(sing_dir, assembly['assembly_patterns']['assembly_pattern'])
assembly_quast_output = join(data_dir, assembly['assembly_patterns']['quast_pattern'])
assembly_quast_output_sing = join(sing_dir, assembly['assembly_patterns']['quast_pattern'])

quast_image = container_image_name(biocontainers, 'quast')


rule assembly_statistics_quast:
    """
    Compute assembly statistics with quast
    """
    input:
        assembly_quast_input
    output:
        directory(assembly_quast_output)
    params:
        quast_input_sing = assembly_quast_input_sing,
        quast_output_sing = assembly_quast_output_sing
    message: 
        '--- Calculating assembly statistics with quast'
    singularity:
        quast_image
    shell:
        'quast.py {params.quast_input_sing} -o {params.quast_output_sing}'



###################################
# Assembly: multiqc statistics

assembly_multiqc_input_pattern = assembly_quast_output + assembly['assembly_patterns']['multiqc_input_file']
assembly_multiqc_input_pattern_sing = assembly_quast_output_sing + assembly['assembly_patterns']['multiqc_input_file']
assembly_multiqc_output_dir = join(data_dir, assembly['assembly_patterns']['assembly_multiqc_pattern_report'])
assembly_multiqc_output_dir = assembly_multiqc_output_dir + '_data'
assembly_multiqc_output_dir_sing = join(sing_dir, assembly['assembly_patterns']['assembly_multiqc_pattern_report'])
assembly_multiqc_output_file_sing = join(sing_dir, assembly['assembly_patterns']['assembly_multiqc_pattern_report'])

multiqc_image = container_image_name(biocontainers, 'multiqc')

def assembly_expand_multiqc_input(wildcards):
    """
    Return input list for multiqc input
    """
    wildcards.qual = workflows['assembly_workflow_all']['qual']
    multiqc_input = expand(assembly_multiqc_input_pattern, sample = wildcards.sample, qual=wildcards.qual, assembler = wildcards.assembler)
    return multiqc_input


def expand_assembly_multiqc_input_sing(wildcards):
    """
    Return input list for multiqc input for sing image
    """
    wildcards.qual = workflows['assembly_workflow_all']['qual']
    multiqc_input = expand(assembly_multiqc_input_pattern_sing, sample = wildcards.sample, qual=wildcards.qual, assembler = wildcards.assembler)
    return multiqc_input


def expand_assembly_multiqc_output_file_sing(wildcards):
    output_file = expand(assembly_multiqc_output_file_sing, sample = wildcards.sample, assembler = wildcards.assembler)
    return output_file


rule assembly_statistics_multiqc:
    """
    Compute assembly statistics with multiqc
    """
    input:
        assembly_expand_multiqc_input 
    output:
        directory(assembly_multiqc_output_dir)
    params:
        input_files = expand_assembly_multiqc_input_sing,
        output_files = expand_assembly_multiqc_output_file_sing,
        output_dir = assembly_multiqc_output_dir_sing
    message: 
        '--- Compiling assembly statistics with multiqc'
    singularity:
        multiqc_image
    shell:
        'multiqc {params.input_files} -n {params.output_files} -o {params.output_dir}'



###################################
# Assembly: build rules

workflows = config['workflows']

directions = [readfilt['direction_labels']['forward'],
              readfilt['direction_labels']['reverse']]


rule assembly_workflow_metaspades:
    """
    Build rule: trigger the metaspades assembly step.
    """
    input:
        expand( assembly_metaspades_output,
                sample    = workflows['assembly_workflow_metaspades']['sample'],
                qual      = workflows['assembly_workflow_metaspades']['qual'],
                direction = directions,
        )

rule assembly_workflow_megahit:
    """
    Build rule: trigger the megahit assembly step.
    """
    input:
        expand( assembly_megahit_output,
                sample    = workflows['assembly_workflow_megahit']['sample'],
                qual      = workflows['assembly_workflow_megahit']['qual'],
                direction = directions,
        )

rule assembly_workflow_all:
    """
    Build rule: trigger the assembly step with all assemblers.
    """
    input:
        expand( assembly_metaspades_output,
                sample    = workflows['assembly_workflow_all']['sample'],
                qual      = workflows['assembly_workflow_all']['qual'],
                direction = directions,
        ),
        expand( assembly_megahit_output,
                sample    = workflows['assembly_workflow_all']['sample'],
                qual      = workflows['assembly_workflow_all']['qual'],
                direction = directions,
        )

rule assembly_workflow_quast:
    input:
        expand( assembly_quast_output,
                sample    = workflows['assembly_workflow_quast']['sample'],
                qual      = workflows['assembly_workflow_quast']['qual'],
                assembler = workflows['assembly_workflow_quast']['assembler'],
        )

rule assembly_workflow_multiqc:
    input:
        expand( assembly_multiqc_output_dir,
                sample    = workflows['assembly_workflow_multiqc']['sample'],
                assembler = workflows['assembly_workflow_multiqc']['multiqc_assembler'],
        )